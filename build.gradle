plugins {
    id 'java'
    id 'groovy'
    id 'jacoco'
}

group 'org.example'
version '1.0-SNAPSHOT'

repositories {
    mavenCentral()
}

dependencies {
    implementation 'com.sparkjava:spark-core:2.9.3'
    implementation 'dev.morphia.morphia:morphia-core:2.2.1'
    implementation 'com.mercadopago:sdk-java:1.11.0'
    implementation 'org.slf4j:slf4j-simple:2.0.0-alpha5'
    implementation 'org.mapstruct:mapstruct:1.4.2.Final'
    implementation 'org.codehaus.groovy:groovy-all:3.0.8'

    testImplementation 'junit:junit:4.13.2'
    testImplementation 'org.mockito:mockito-core:4.0.0'
    testImplementation 'org.codehaus.groovy:groovy-all:3.0.8'
    testImplementation 'org.spockframework:spock-core:2.0-groovy-3.0'

    annotationProcessor 'org.mapstruct:mapstruct-processor:1.4.2.Final'
}

test {
    useJUnitPlatform()
    testLogging {
        showStandardStreams = false
    }
}

jacocoTestReport {
    reports {
        xml.enabled true
        html.enabled true
    }
}

check.dependsOn jacocoTestReport

// Task to open the report generated by Jacoco
task openReport {
    doLast {
        exec {
            executable 'open'
            args 'build/reports/jacoco/test/html/index.html'
        }
    }
}

// Task to clean the directory, run tests, generate the Jacoco report and open it
task testAndReport {
    dependsOn 'clean'
    dependsOn 'test'
    dependsOn 'jacocoTestReport'
    dependsOn 'openReport'
    tasks.findByName('test').mustRunAfter 'clean'
    tasks.findByName('jacocoTestReport').mustRunAfter 'test'
    tasks.findByName('openReport').mustRunAfter 'jacocoTestReport'
}

//To run tests with java classes using groovy classes is necessary compile first groovy files and then java files
//You should remove this config if groovy classes were removed
sourceSets {
    main {
        java {
            srcDirs = [] // don't compile Java code twice
        }
        groovy {
            srcDirs = [ 'src/main/groovy', 'src/main/java' ]
        }
    }
}
